import java.util.regex.Matcher
import java.util.regex.Pattern

/*
 * Copyright (c) 2016.  The AppCan Open Source Project.
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU Lesser General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU Lesser General Public License for more details.
 *
 *  You should have received a copy of the GNU Lesser General Public License
 *  along with this program.  If not, see <http://www.gnu.org/licenses/>.
 *
 *
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: 'com.android.application'

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile(name: "Engine-system-debug-4.0.0", ext: 'aar')
//    compile project(':appcan-android:Engine')
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 14 //
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs=['libs']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}



//-----------------------------------以下为生成插件Zip包部分---------------------------------

def pluginName = project.name
def projectDir=project.getProjectDir().absolutePath
def jarDir = pluginName + '/jar/'
def jarName = "plugin_" + pluginName + ".jar"
def jarPath = jarDir + jarName

task clearJar(type: Delete) {
    delete jarPath;
}

task buildPlugin(type: Jar, dependsOn: ['compileReleaseJavaWithJavac']) {
    def applicationId = getApplicationId();
    archiveName jarName
    from('build/intermediates/classes/release/')
    destinationDir = file(jarDir)
    exclude('**/R.class')
    exclude('**/R\$*.class')
    exclude('**/BuildConfig.class')
    if (applicationId != null) {
        exclude(applicationId.replace('.', '/'))
    }
}

buildPlugin.dependsOn(clearJar)

//获取应用Id
def getApplicationId() {
    return getAndroidPlugin().extension.defaultConfig.applicationId;
}

def getAndroidPlugin(){
    def plugin = project.plugins.findPlugin('com.android.application') ?:
            project.plugins.findPlugin('com.android.library')
    return plugin as com.android.build.gradle.BasePlugin
}

//获取插件版本号
def getPluginVersion(String pluginName) {
    def version = ''
    Pattern p = Pattern.compile("version=\"(.*?)\"")
    Matcher m = p.matcher(new File(project.getProjectDir(), pluginName + "/info.xml")
            .getText('UTF-8'))
    m.find()
    if (m.find()) {
        version = m.group(1)
        println(pluginName + " version: " + version)
    }
    return version
}


buildPlugin.doLast{
    def argsList =["--dex","--verbose","--no-strict","--output=$projectDir/$pluginName/dex/plugin_${pluginName}_dex.jar"]
    new File(projectDir,"$pluginName/jar").listFiles().each { file ->
        argsList.add(file.absolutePath)
        println(file.absolutePath)
    }
    println(argsList)
    def dexJarTask=tasks.create("buildPluginTemp",Exec){
        workingDir "./$pluginName"
        def androidSDKDir = getAndroidPlugin().sdkHandler.getSdkFolder().absolutePath
        def androidToolDir=androidSDKDir+'/build-tools/'+"${android.buildToolsVersion}"+'/'
        commandLine androidToolDir+"dx"
        args argsList
        standardOutput = new ByteArrayOutputStream()
    }
    dexJarTask.doFirst{
        println '== dexJars start=='
        File dexDir=new File(projectDir,"$pluginName/dex")
        if (!dexDir.exists()){
            dexDir.mkdir()
        }
    }
    dexJarTask.doLast{
        println standardOutput
        println '== dexJars  end=='
        def zipTask=tasks.findByName("zipPlugin")
        zipTask.execute()
    }
    dexJarTask.execute()
}

//生成插件包
task zipPlugin(type: Zip) {
    baseName pluginName + "-" + getPluginVersion(pluginName)
    from(pluginName)
    into(pluginName)
    destinationDir = file('.');
}
